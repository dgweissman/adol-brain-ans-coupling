title:
 Effect of brain-autonomic coupling on mean autonomic activity
 
data:
FILE = faces.physio.threat0.082718.csv;

  variable:
  names are
SID	block	wide	SCRc	RSAc	
vmPFCc	dmPFCc	female	
MFQ	CDI	SCARED	
NCrimeM	SCrimeM	BullyM	DiscrimM	
W1GA W1MD;

USEVARIABLES = 
 block SCRc RSAc vmPFCc dmPFCc 
 female 
 NCrimeM SCrimeM	BullyM	DiscrimM
 MFQ CDI SCARED W1GA W1MD;
within = block SCRc RSAc vmPFCc dmPFCc;
between = 
female NCrimeM SCrimeM	BullyM	DiscrimM
MFQ CDI SCARED W1GA W1MD;

CLUSTER = SID;

MISSING = .;
	
ANALYSIS:
	coverage = 0;
	!model = nocovariances;
	TYPE = TWOLEVEL RANDOM;
	ESTIMATOR = ML;
	ALGORITHM=INTEGRATION;

MODEL:

%WITHIN%

dmPFCc vmPFCc RSAc SCRc ON block;

S1|SCRc ON dmPFCc;

[SCRc@0 dmPFCc@0];

S2|RSAc ON vmPFCc;

[RSAc@0 vmPFCc@0];

dmPFCc with vmPFCc;

RSAc with SCRc;

 %BETWEEN%

T by NCrimeM SCrimeM BullyM DiscrimM;

!T2| T XWITH T;

I1 by W1GA W1MD;

I by MFQ CDI SCARED;

TS1| T XWITH S1;

TS2| T XWITH S2;

S1; [S1];

S2; [S2];

S1 on T female;

S2 on T female;

I on T I1 S1 S2 TS1 TS2 female;

OUTPUT: SAMPSTAT CINTERVAL;
SAVE:
File is fscores.txt;
SAVE = FSCORES;